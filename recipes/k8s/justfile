prod-conf := "$HOME/.kube/prod.config"
test-conf := "$HOME/.kube/test.config"
dev-conf  := "$HOME/.kube/dev.config"
create-test-ns:
	kubectl create namespace test
exec-pod *args: create-test-ns
	python ~/.recipes/k8s/k8s.py exec-pod {{args}}
attach-debug *args:
	python ~/.recipes/k8s/k8s.py attach-debug {{args}}

install-cert-mgr:
	helm repo add jetstack https://charts.jetstack.io

	helm repo update

	- helm upgrade --install cert-manager jetstack/cert-manager \
		--namespace cert-manager \
		--create-namespace


install-rancher: install-cert-mgr
	helm repo add rancher-latest https://releases.rancher.com/server-charts/latest

	- kubectl create namespace cattle-system

	kubectl apply -f https://github.com/cert-manager/cert-manager/releases/download/v1.12.6/cert-manager.crds.yaml

	helm install rancher rancher-latest/rancher \
		--namespace cattle-system \
		--set hostname=rancher.192.168.124.13.sslip.io \
		--set replicas=1 \
		--set bootstrapPassword=admin

cilium-test:
	cilium connectivity test --request-timeout 30s --connect-timeout 10s

install-dapr:
	helm upgrade --install dapr dapr/dapr \
		--version=1.12 \
		--namespace dapr-system \
		--create-namespace \
		--wait
	helm install dapr-dashboard dapr/dapr-dashboard --namespace dapr-system

install-image-proxy:
	export LATEST=$(basename $(curl -s -w %{redirect_url} https://github.com/ketches/registry-proxy/releases/latest)) && \
	kubectl apply -f https://raw.githubusercontent.com/ketches/registry-proxy/$LATEST/deploy/manifests.yaml
	kubectl apply -f regproxy.yaml

debug-pod *args: create-test-ns
	python ~/.recipes/k8s/k8s.py pod-debug {{args}}

set-prod:
	rm $HOME/.kube/config && ln -s {{prod-conf}} $HOME/.kube/config

set-test:
	rm $HOME/.kube/config && ln -s {{test-conf}} $HOME/.kube/config

set-dev:
	rm $HOME/.kube/config && ln -s {{dev-conf}} $HOME/.kube/config

prod-k9s: set-prod
	k9s

test-k9s: set-test
	k9s
	